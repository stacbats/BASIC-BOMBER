30 POKE53281,5: POKE53280,12
95 PRINT"{CLEAR}","{RED}{DOWN*2}{RIGHT*2}{reverse on}*{reverse off}{LIGHT GREEN} port stanley {RED}{reverse on}*{reverse off}"
100 REM *********** DISPLAY BUILDINGS  ***********
102 SC = 1024                   : REM 1ST SCREEN ADDRESS
105 FOR CO = 0 TO 39            : REM SETTING UP VARIABLE   CO(COLLUMNS)
106 BU=INT(RND(5)*7)+18         : REM create different skySKrapers
107 BL=INT(RND(7)*2)*88+162     : REM OUR BLOCKS
108 FOR RO = BU TO 24           : REM SETTING UP VARIABLE   RO(ROWS)
109 PL = SC + RO * 40 +CO       : REM SETTING UP VARIABLE   PL(PLACE)
111 POKE PL,BL                  : REM 160 IS THE SOLID BLOCK CHAR
113 POKE PL+54272,11
115 NEXT:NEXT
117 FORF= 1984 TO 2023: POKEF,160:POKEF+54272,11:NEXT
200 REM *********** PLANE MOVEMENT  ***********
202 RO=4:CO =0
204 PL=SC+RO*40+CO 
214 POKE PL-1,32:POKEPL,32:PL=PL+1
215 KE=PEEK(197):IF KE =4 THEN PL=PL-40
216 IF PL<1024 THEN PL=PL+40
217 IF KE=5 THEN PL=PL+40
218 LO=LO+1:            REM * LOOP COUNT
220 IF PEEK(PL)<>32 THEN 600
240 POKEPL,91:POKE PL-1,109
242 POKEPL+54272,0:POKE PL+54272-1,0
250 IF PEEK(PL+40)<>160 THEN 300
260 TA = TA+1:REM ************ TAXI COUNT
261 FORF=1 TO 20*TA:NEXT
262 IF TA < 15 THEN 300
270 PRINT"{black}{home}{down*6}{right*3}landed safely!":GOTO 800
299 REM *********** BOMB RELEASE **************
300 REM BOMB
302 IF BO=0 THEN 350
304 IF DR THEN BP=PL:DR=0: REM BP = BOMB POSITION
310 POKE BP,32
314 BP = BP + 40
317 IF PEEK(BP)=160 THEN BO = 0
318 IF BO = 0 THEN 350
320 POKE BP,46:POKE BP+54272,0
350 REM *********** CHECK CONTROLS
356 KE=PEEK(197)
360 IF KE=3 AND BO=0 THEN DR=1:BO=1
400 GOTO 214
600 REM *********** CRASH ***********
602 IF PL <1500 THEN PRINT"lost in the clouds":STOP
605 POKE PL,42: POKE PL + 54272,0: REM EXPLODE SIGNAL
610 PRINT "{down*4}{right*8}{YELLOW} boom noise "
615 PRINT "{ORANGE}{right*8}  crashed  "

800 REM FINISH
802 PA=INT(LO/40):      REM 1 PASS = 40 loops
803 GOSUB840
804 PRINT"{down*7}{right*3}{cyan}your score = "(40-PA)
840 FOR F =1944 TO 1983
842 IF PEEK(F)<>32 THEN LE=LE+l
844 NEXT:LE=LE-2:RETURN
 